[TOC]
### 1. 概述
1. $random —— 系统随机化调用函数，返回32bit有符号数；
2. $urandom() —— 系统随机化调用函数，返回32bit无符号数；
3. $urandom_range()—— 系统随机化调用函数，返回指定范围内的无符号随机整数；
4. srandom() —— 对象和线程（手动添加种子seed）的随机化方法；
5. randomize() —— 对象的随机化方法；
6. std:randomize() -- 范围随机函数，为当前范围内的变量随机赋值
  
### 2. 基础
#### 1. $random——系统方法，返回32bit的有符号伪随机数
语法如下：
   ~~~
   random(seed)
   /种子变量seed是可选的。
   ~~~
$random产生一个32bit的有符号随机数，可以是正数、也可以是负数。其种子变量（必须是reg类型、整型或时间型变量）。
1. $random%b; b为一个大于0的整数，表达式给出了一个范围在[-b+1 ： b-1]之间的随机数
   ~~~
   int randval;
   randval = $random % 60;      //随机产生一个-59~59之间的有符号数
   ~~~
2. **{ }化为无符号数**,{\$random%b};拼接操作符{ }将$random返回的有符号数转换成了无符号数。
   ~~~
   int randval;
   randval = {$random % 60};      //随机产生一个0~59之间的无符号数
   ~~~

#### 2. $urandom——系统方法，返回32bit的无符号伪随机数
语法如下
~~~
function int unsigned $urandom(int seed);
//种子seed是可选参数，决定了生成的随机数值。相同的种子生成的随机数值也相同
~~~
示例
~~~
bit[64:1]   addr;
bit [3:0]   number;

addr[32:1] = $urandom(254) ;      //初始化随机数发生器（RNG），获得一个32bit的随机数

addr = {$urandom, $urandom};     //产生一个64bit的随机数
number = $urandom & 15;          //产生一个4bit的随机数
~~~

#### 3. $urandom_range()——系统方法，返回指定范围内的无符号随机整数
语法如下：
~~~
function int unsigned $urandom_range(int unsigned maxval,
                                     int unsigned minval = 0);
//参数最小值min是可以省略的 ， 且最大值与最小值的位置是可以互换的。
~~~
下述三个随机值都在0~7的范围内，示例：
~~~
val1 = $urandom_range(7,0) ;
val2 = $urandom_range(7) ;
val3 = $urandom_range(0, 7) ;
~~~

#### 4. srandom()——对象方法，在类方法内/外为随机数据发生器（RNG）添加随机种子
语法如下：
~~~
function void srandom(int seed);
//种子seed需要手动添加，来初始化随机数据发生器（RNG）
~~~
示例1：类内添加seed
~~~
class Packet;
  rand bit[15:0]  header;

  function new(int seed) ;
    this.srandom(seed) ;
    ...
  endfunction
endclass
~~~

示例2：类外添加seed
~~~
Packet p=new(200) ;    //通过种子200，创建对象p
p.srandom(300) ;       //通过种子300，重新创建p
~~~

#### 5. randomize() —— 对象方法，为对象中的随机化变量随机赋值
语法如下：
~~~
virtual function int randomize();
//该方法是一个虚方法，会为对象中的所有随机变量产生随机值。如果randomize()成功的为对象中的所有随机变量赋随机值，则返回1，否则返回0.
~~~

示例1：成功赋值返回值为1
~~~
class SimpleSum;
  rand bit[7:0]  x, y, z;
  constraint c{ z== x+y}; 
endclass

SimpleSum p=new() ;
int success=p.randomize() ;      //随机化成功，则返回1，即success为1
if(success==1) ...

~~~

示例2：内嵌约束randomize() with{ }
~~~
class SimpleSum;
  rand bit[7:0]  x, y, z;
  constraint c{ z== x+y}; 
endclass

task InlineConstraintDemo(SimpleSum p) ;
  int success;
  success = p.randomize() with {x<y;} ;      //随机化成功，则返回1，即success为1
endtask

~~~
#### 6. std::randomize() —— 范围随机函数，为当前范围内的变量随机赋值
1. 语法
~~~
[std::]randomize([variable_identifier_list]) [with constraint_block]
//范围随机化函数与类的随机化方法的作用相同，只是它操作于当前范围内的变量而不是类成员变量。函数的参数指定了那些需要赋值为随机值的变量，也就是随机变量。
//区别于对象随机化方法randomize()要求的变量必须为rand类型；std::randomize()可以随机化非rand变量 
~~~

2. 成功随机普通非rand类型变量,eg1:
~~~
module stim;
 bit [15:0] addr;
 bit [31:0] data;
	 function bit gen_stim();
		 bit success, rd_wr;                     //注意，addr 和 data 具有模块级的作用范围，而rd_wr具有函数内的本地作用范围。
		 success = std::randomize(addr, data, rd_wr); // 调用 std::randomize（std::可省略, 一般不忽略写法）
		 return rd_wr;                           //如果范围随机化成功，那么它返回 1；否则它返回 0。
	 endfunction                                 //如果范围随机化函数未指定参数，则作为一个检查器使用，并简单地返回状态。
 ...
endmodule

//函数 gen_stim 调用 std::randomize()函数并带有三个变量作为参数：addr、data、和 rd_wr，std::randomize()将新的随机变量赋值到那
//些在 gen_stim 函数范围内可见的变量。
~~~

3. 成功随机普通非rand类型变量并施加约束,eg2:
~~~
task stimulus (int length);
 int a, b, c, success;
 success = std::randomize(a, b, c) with {a < b; a + b < length};
 ...
 success = std::randomize(a, b) with {b - a > length};
 ...
endtask
~~~

### 2. unique的随机控制
#### 1. unique约束我们可以完成以下任务
   1. 在变量之间生成唯一的值
   2. 在数组中生成唯一元素(固定大小数组、动态数组、关联数组和队列)
#### 2. 唯一约束的例子
##### 1. 产生唯一的元素
1.eg
~~~
class unique_elements;
  rand bit [3:0] var_1,var_2,var_3;
  rand bit [7:0] array[6];
  constraint varis_c {unique {var_1,var_2,var_3};}
  constraint array_c {unique {array};}
  
  function void display();
    $display("var_1 = %p",var_1);
    $display("var_2 = %p",var_2);
    $display("var_3 = %p",var_3);
    $display("array = %p",array);
  endfunction
endclass
 
program unique_elements_randomization;
  unique_elements pkt;
 
  initial begin
    pkt = new();
    pkt.randomize();
    pkt.display();   
  end
endprogram
~~~
2. rsl
~~~
var_1 =  8 
var_2 = 14
var_3 = 11
array = '{'h81, 'h7b, 'h4, 'h47, 'he1, 'h17}
~~~
##### 2. 元素的唯一数组示例
1. eg
~~~
lass unique_elements;
  rand bit [31:0] array[10];
  
  constraint array_c {
    unique {array};
    foreach(array[i]) {
      array[i] < 10;
    }
  }
 
  function void display();
    $display("array = %p", array);
  endfunction
 
endclass
 
program unique_elements_randomization;
  unique_elements pkt;
 
  initial begin
    pkt = new();
    pkt.randomize();
    pkt.display();   
  end
  
endprogram
~~~

2. rsl
~~~
array = '{'h5, 'h7, 'h8, 'h1, 'h6, 'h9, 'h2, 'h3, 'h4, 'h0}
~~~

#### 3. unique if应用实例一
1. in the below example,More than one condition is true.value of a=10, b=20 and c=40. conditions a<b and a<c are true, Therefore on execution, simulator issue a run time warning.“RT Warning: More than one condition match in ‘unique if’ statement.”
2. eg
~~~
module unique_if;
  //variables declaration
  int a,b,c;
 
   initial begin
     //initialization
     a=10;
     b=20;
     c=40;
 
     unique if ( a < b ) $display("\t a is less than b");
     else   if ( a < c ) $display("\t a is less than c");
     else                $display("\t a is greater than b and c");
  end
endmodule
~~~
3. rsl
~~~
a is less than b
RT Warning: More than one conditions match in 'unique if' statement.
~~~

#### 4. 三、unique if应用实例二
1. In below example,No condition is true and final if doesn’t have corresponding else.value of a=50, b=20 and c=40, conditions a<b and a<c are false,Therefore on execution, simulator issue a run time warning.“RT Warning: No condition matches in ‘unique if’ statement.”
2. eg
~~~
module unique_if;
  //variables declaration
  int a,b,c;
 
   initial begin
     //initialization
     a=50;
     b=20;
     c=40;
    
     unique if ( a < b ) $display("\t a is less than b");
     else   if ( a < c ) $display("\t a is less than c");
  end
     
endmodule
~~~
3. rsl
~~~
RT Warning: No condition matches in 'unique if' statement
~~~
#### 5. 三、unique if应用实例二
1. In below example, value of a=50, b=20 and c=40.conditions a<b and a<c are false, so else part is true, there is no simulator run time warning.
2. eg
~~~
module unique_if;
 
  //variables declaration
  int a,b,c;
 
   initial begin
     //initialization
     a=50;
     b=20;
     c=40;
    
     priority if ( a < b ) $display("\t a is less than b");
     else     if ( a < c ) $display("\t a is less than c");
     else                  $display("\t a is greater than b and c");
  end
    
endmodule
~~~
3. rsl
~~~
a is greater than b and c
~~~

### 3. 传送门
  1. [随机化系统函数和随机化方法](https://blog.csdn.net/weixin_46022434/article/details/107722106)
  2. [SystemVerilog unique array and unique if](https://blog.csdn.net/gsjthxy/article/details/105126165)